plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
}

apply plugin: 'maven'
apply plugin: 'signing'

group = "ch.confinale"
version = "1.0"
archivesBaseName = "addressr"

repositories {
    mavenCentral()
    mavenLocal()
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.yaml:snakeyaml:1.25")
    implementation("com.github.spullara.mustache.java:compiler:0.9.6")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.5.2")
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name = 'addressr'
                description = 'A library to format addresses in its locale format'
                url = 'https://github.com/confinale/addressr.git/'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'cn-wolski'
                        name = 'Michael Wolski'
                        email = 'michael.wolski@confinale.ch'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com:confinale/addressr.git'
                    developerConnection = 'scm:git:ssh:git@github.com:confinale/addressr.git'
                    url = 'https://github.com/confinale/addressr.git/'
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

signing {
    sign configurations.archives
}

artifacts {
    archives javadocJar, sourcesJar
}

tasks.withType(Test) {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}